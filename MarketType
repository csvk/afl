_SECTION_BEGIN("Market Type");

TimeFrameSet(inWeekly);
WH = H;
WL = L;
WC = C;
WV = ( WH - WL ) / WC * 100;
TimeFrameRestore();

WVDaily = TimeFrameExpand(WV, inWeekly);
WeeklyVolatility = Ref(WVDaily, -1);

//Based on all data till Aug 2018
//WV Mean	4.407698276
//SD	2.808484982
//+1SD	7.216183257
//-1SD	1.599213294
//+0.5SD	5.811940767
//-0.5SD	3.003455785
//+3SD	12.83315322

//MarketType = 1 "Quite", 2 "Normal", 3 "Volatile", 4 "Highly Volatile"

MarketType = IIf(WeeklyVolatility < 3, 1, IIf(WeeklyVolatility < 6, 2, IIf(WeeklyVolatility < 13, 3, 4)));
MQ = ParamColor( "Quite", colorDarkGreen );
MN = ParamColor( "Normal", colorGreen );
MV = ParamColor( "Volatile", colorYellow );
MHV = ParamColor( "Highly Volatile", colorRed );

MarketTypeColor = IIf(WeeklyVolatility < 3, MQ, IIf(WeeklyVolatility < 6, MN, IIf(WeeklyVolatility < 13, MV, MHV)));
Plot(WeeklyVolatility, "WeeklyVolatility", MarketTypeColor, ParamStyle("Volatility Style", styleThick),0,10);


Price = ParamField("Price field", 3);
//Periods = Param("Periods", 100, 10, 200, 1, 10 );
Periods = StrToNum(ParamList("Periods", "20|60|100|200"));

PrevPrice = Ref(Price, -Periods); 
RangeChange = (Price - PrevPrice) / PrevPrice * 100;

RangeChange20_Mean = 1;
RangeChange60_Mean = 3;
RangeChange100_Mean = 5.5;
RangeChange200_Mean = 11.5;
RangeChange20_Std = 7;
RangeChange60_Std = 12.5;
RangeChange100_Std = 17;
RangeChange200_Std = 25;

if (Periods == 20) {
	RCMean = RangeChange20_Mean;
	RCStd = RangeChange20_Std;
}
else {
	if (Periods == 60) {
		RCMean = RangeChange60_Mean;
		RCStd = RangeChange60_Std;
	}
	else {
			if (Periods == 100) {
				RCMean = RangeChange100_Mean;
				RCStd = RangeChange100_Std;
			}
			else {
				RCMean = RangeChange200_Mean;
				RCStd = RangeChange200_Std;
			}
	}
}

//MarketDirection = 1 "Strong Bull", 2 "Bull", 3 "Neutral High", 4 "Neutral Low", 5 "Bear", 6 "Strong Bear"
MarketDirection = IIf(RangeChange < RCMean - RCStd * 1.5, 6, // Strong Bear
                               IIf(RangeChange >= RCMean - RCStd * 1.5 AND RangeChange <= 0, 5, // Bear
                                   IIf(RangeChange > 0 AND RangeChange < RCMean + RCStd * 0.5, 4, // Neutral Low
									   IIf(RangeChange >= RCMean + RCStd * 0.5 AND RangeChange < RCMean + RCStd, 3, // Neutral High
										   IIf(RangeChange >= RCMean + RCStd AND RangeChange < RCMean + RCStd * 3, 2, 1))))); // Bull / Strong Bull
                           
MNL = ParamColor( "Neutral Low", colorLightGrey );
MNH = ParamColor( "Neutral High", colorYellow );
MB = ParamColor( "Bull", colorDarkGreen );
MSB = ParamColor( "Strong Bull", colorGreen );
MBR = ParamColor( "Bear", colorDarkRed );
MSBR = ParamColor( "Strong Bear", colorRed );

MarketDirectionColor = IIf(RangeChange < RCMean - RCStd * 1.5, MSBR, // Strong Bear
                               IIf(RangeChange >= RCMean - RCStd * 1.5 AND RangeChange <= 0, MBR, // Bear
                                   IIf(RangeChange > 0 AND RangeChange < RCMean + RCStd * 0.5, MNL, // Neutral Low
									   IIf(RangeChange >= RCMean + RCStd * 0.5 AND RangeChange < RCMean + RCStd, MNH, // Neutral High
										   IIf(RangeChange >= RCMean + RCStd AND RangeChange < RCMean + RCStd * 3, MB, MSB))))); // Bull / Strong Bull

Plot(RangeChange, "MarketDirection", MarketDirectionColor, ParamStyle("Direction Style", styleDashed),0,10);

_SECTION_END();

Filter = 1;
SetOption("NoDefaultColumns", True);
AddTextColumn(Name(), "SYMBOL");
AddColumn(DateTime(), "DATE", formatDateTime);
AddColumn( O, "Open"); 	
AddColumn( C, "Close"); 
AddColumn( H, "High"); 
AddColumn( L, "Low"); 
AddColumn( WeeklyVolatility, "WeeklyVolatility"); 
AddColumn( MarketType, "MarketType");
AddColumn( RangeChange, "RangeChange (" + Periods + ")");
AddColumn( MarketDirection, "MarketDirection (" + Periods + ")");
