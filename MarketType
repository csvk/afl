_SECTION_BEGIN("Market Type");

TimeFrameSet(inWeekly);
WH = H;
WL = L;
WC = C;
WV = ( WH - WL ) / WC * 100;
TimeFrameRestore();

WVDaily = TimeFrameExpand(WV, inWeekly);
WeeklyVolatility = Ref(WVDaily, -1);

//Based on all data till Aug 2018
//WV Mean	4.407698276
//SD	2.808484982
//+1SD	7.216183257
//-1SD	1.599213294
//+0.5SD	5.811940767
//-0.5SD	3.003455785
//+3SD	12.83315322

//MarketType = 1 "Quite", 2 "Normal", 3 "Volatile", 4 "Highly Volatile"

MarketType = IIf(WeeklyVolatility < 3, 1, IIf(WeeklyVolatility < 6, 2, IIf(WeeklyVolatility < 13, 3, 4)));
MQ = ParamColor( "Quite", colorCycle );
MN = ParamColor( "Normal", colorCycle );
MV = ParamColor( "Volatile", colorCycle );
MHV = ParamColor( "Highly Volatile", colorCycle );

MarketTypeColor = IIf(WeeklyVolatility < 3, MQ, IIf(WeeklyVolatility < 6, MN, IIf(WeeklyVolatility < 13, MV, MHV)));
Plot(WeeklyVolatility, "WeeklyVolatility", MarketTypeColor, ParamStyle("Volatility Style"),0,10);


Price = ParamField("Price field", 3);
Periods = Param("Periods", 100, 10, 200, 1, 10 );

PrevPrice = Ref(Price, -Periods); 
RangeChange = (Price - PrevPrice) / PrevPrice * 100;

MN = ParamColor( "Neutral", colorCycle );
MB = ParamColor( "Bull", colorCycle );
MSB = ParamColor( "Strong Bull", colorCycle );
MBR = ParamColor( "Bear", colorCycle );
MSBR = ParamColor( "Strong Bear", colorCycle );

MarketDirectionColor = IIf(RangeChange > 0 AND RangeChange <=4 , MN, // Neutral
                           IIf(RangeChange > 4 AND RangeChange < 15, MB, // Bull
                               IIf(RangeChange > 15, MSB, // Strong Bull
                                   IIf(RangeChange <= 0 AND RangeChange > -13, MBR, MSBR)))); // Bear / Strong Bear

Plot(RangeChange, "MarketDirection", MarketDirectionColor, ParamStyle("Direction Style"),0,10);

_SECTION_END();

Filter = 1;
SetOption("NoDefaultColumns", True);
AddTextColumn(Name(), "SYMBOL");
AddColumn(DateTime(), "DATE", formatDateTime);
AddColumn( O, "Open"); 	
AddColumn( C, "Close"); 
AddColumn( H, "High"); 
AddColumn( L, "Low"); 
AddColumn( WeeklyVolatility, "WeeklyVolatility"); 
AddColumn( MarketType, "MarketType");
AddColumn( RangeChange, "RangeChange (" + Periods + ")");
